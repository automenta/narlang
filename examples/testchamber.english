*motorbabbling=false
//background knowledge:
<{SELF} <-> nars>.
<((<($1 * #1) --> isin> &/ <(nars * #1) --> isin>) &/ <({SELF} * $1) --> ^pick>) =/> <(nars * $1) --> pick>>.
<((<(nars * $2) --> isin> &/ <(nars * $1) --> pick>) &/ <({SELF} * $1) --> ^drop>) =/> <($1 * $2) --> isin>>.
<(<(nars * #1) --> isin> &/ <({SELF} * $1) --> ^go>) =/> <(nars * $1) --> isin>>.
//all the things nars can pick:
*ground=<(nars * cat) --> pick>
//all the places nars and the cat can be in:
*ground=<(nars * kitchen) --> isin>
*ground=<(nars * bedroom) --> isin>
*ground=<(cat * kitchen) --> isin>
*ground=<(cat * bedroom) --> isin>

//t=1 observations:
<(cat * kitchen) --> isin>. :|:
<(nars * bedroom) --> isin>. :|:

nars, the cat should be in the bedroom!
//expected: ^go executed with args kitchen
10

//t=2 observations:
<(cat * kitchen) --> isin>. :|:
<(nars * kitchen) --> isin>. :|:

//continue with the same goal:
*step
//expected: ^pick executed with args cat
10

where is nars most recently?
//expected: kitchen
what happened to the cat most recently?
//expected: the cat was picked up
what did nars do to the cat most recently?
//expected: nars picked up the cat

//t=3 observations:
<(nars * kitchen) --> isin>. :|:
<(nars * cat) --> pick>. :|:

*step
//expected: ^go executed with args bedroom
10

//t=4 observations:
<(nars * bedroom) --> isin>. :|:
<(nars * cat) --> pick>. :|:

*step
//expected: ^drop executed with args cat
